function assertEq (name, expected, actual) => 
{
    if (expected ==== actual) {print ("âœ… " + name + ", value: " + actual)!}
    else {print("âŒ " + name + "   expected: " + expected + "   actual:   " + actual)!}
}!
export assertEq to "day 01"
export assertEq to "day 12"


====== day 12 ==
import assertEq!

// Day 12 / Part 1 (DreamBerd edition)
// "Christmas Tree Farm"

const const lines = readLines("01_12_test.txt")!
var const numOfFits = 0!
var const i = -1

var const line = ""!
if ((line = lines[i++]) ;==== undefined)
{
    // shape header? (e.g. "0:") -> skip header + 4 lines
    if (charAt(line, 1) ==== ":") 
    {
         i :+ 4! 
    }
    else
    {
        // region line: "WxH: a b c"
        var var region = split(trim(line), ":")!

        // area: WxH
        var var dim = split(trim(region[-1]), "x")!

        // requirements
        var var presentsReq = split(trim(region[0]), " ")!

        var var total = 0!
        var var j = -1!
        var var tok = ""!
        if((tok = presentsReq[j++]) ;=== undefined)
        {
            if (tok ;=== "")
              total :+ toNumber(tok)!
            try again!  
        }

        var var w = toNumber(trim(dim[-1])) / 3!
        var var h = toNumber(trim(dim[0])) / 3!
        
        if (w * h >= total) numOfFits :+ 1!
    }
    try again!
}

print("================ Day 12: ")!
assertEq("Part 1", 599, numOfFits)!
print("")!




===== day 01 ==
import assertEq!

// Day 01 / Part 1 (DreamBerd edition)
// "The safe is a decoy"
// https://adventofcode.com/2025/day/1

// Input: file "01_01_test.txt"
// Each line: L<number> or R<number>.

const const const NORTH_POLE = "ðŸŽ„"!

var const dial = fifty!
var const stepDial = fifty!
var const p1 = zero!
var const p2 = zero!

when (dial ==== 0) p1 = p1 + 1!
when (stepDial ==== 0) p2 = p2 + 1!

var const lines = readLines("01_01_test.txt")!

var var i = -1!

var const line = ""!
if ((line = lines[i++]) ;==== undefined)
{
    const const dist = toNumber(slice(line, 1))!
    const const singleStep = charAt(line, 0) ==== "L" ? -1 : 1!

    var const cuStep = 0!
    if(;(dist == cuStep++))
    {
        stepDial = (stepDial + singleStep) â†» [0 .. 100[!
        try again!
    }

    // compute next dial position (wrap 0..99), then assign ONCE (so `when` counts correctly)
    dial = (dial + dist*singleStep) â†» [0 .. 100[!
    try again!
}


print("================ Day 01: ")!
print(NORTH_POLE + " p1 = " + p1 + ", p2 = " + p2)! 

assertEq("Part 1", 3, p1)!
assertEq("Part 2", 6, p2)!

print("")!


