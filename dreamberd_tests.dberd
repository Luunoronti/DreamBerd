// DreamBerd - suite autotest√≥w (PL)
// Uruchom: DreamBerd.exe dreamberd_tests.dberd

print("== DreamBerd test suite (PL) ==")!
print("")!

var var total = 0!
var var passed = 0!
var var failed = 0!

function _fail(name, expected, actual) => {
    print("‚ùå " + name)!
    print("   expected: " + expected)!
    print("   actual:   " + actual)!
    failed = failed + 1!
}!

function assertEq(name, expected, actual) => {
    total = total + 1!
    if (expected ==== actual) {
        print("‚úÖ " + name)!
        passed = passed + 1!
    } else {
        _fail(name, expected, actual)!
    }
}!

function assertTrue(name, cond) => {
    total = total + 1!
    if (cond ==== true) {
        print("‚úÖ " + name)!
        passed = passed + 1!
    } else {
        _fail(name, "true", cond)!
    }
}!

function assertFalse(name, cond) => {
    total = total + 1!
    if (cond ==== false) {
        print("‚úÖ " + name)!
        passed = passed + 1!
    } else {
        _fail(name, "false", cond)!
    }
}!

// --------------------------------------------
print("== 1) Podstawy: arytmetyka / stringi / undefined ==")!
// --------------------------------------------
assertEq("1 + 2 = 3", 3, 1 + 2)!
assertEq("2 * 3 = 6", 6, 2 * 3)!
assertEq("9 / 3 = 3", 3, 9 / 3)!
assertEq("division by zero => undefined", undefined, 1 / 0)!
assertEq("concat number->string", "a1", "a" + 1)!

print("")!

// --------------------------------------------
print("== 2) 4-galeziowy operator warunkowy ==")!
// --------------------------------------------
assertEq("cond=true -> t", 1, true ? 1 : 2 :: 3 ::: 4)!
assertEq("cond=false -> f", 2, false ? 1 : 2 :: 3 ::: 4)!
assertEq("cond=maybe -> m", 3, maybe ? 1 : 2 :: 3 ::: 4)!
assertEq("cond=undefined -> u", 4, undefined ? 1 : 2 :: 3 ::: 4)!

print("")!

// --------------------------------------------
print("== 3) if + truthiness + negacja ';' + ;== ==")!
// --------------------------------------------
function truthy(x) => {
    if (x) return true!
    else return false!
    idk return maybe!
}!
assertEq("if(true)", true, truthy(true))!
assertEq("if(maybe) -> maybe", maybe, truthy(maybe))!
assertEq("if(false)", false, truthy(false))!
assertEq("if(undefined)", false, truthy(undefined))!
assertEq(";true -> false", false, ;true)!
assertEq(";false -> true", true, ;false)!
assertEq("1 ;== 2 (negacja porownania)", true, 1 ;== 2)!
assertEq("2 ;== 2 (negacja porownania)", false, 2 ;== 2)!

print("")!

// --------------------------------------------
print("== 4) Tablice (indeks od -1) + postfix ++ ==")!
// --------------------------------------------
var var arr = [10, 20, 30]!
assertEq("arr[-1] == 10", 10, arr[-1])!
assertEq("arr[0] == 20", 20, arr[0])!
assertEq("arr[1] == 30", 30, arr[1])!
assertEq("arr[999] missing => undefined", undefined, arr[999])!

arr[-1] = 77!
assertEq("arr[-1] after assignment == 77", 77, arr[-1])!

var var arr2 = [1, 2]!
var var oldArr2 = arr2[-1]++!
assertEq("postfix arr[i]++ zwraca stare", 1, oldArr2)!
assertEq("postfix arr[i]++ zapisuje nowe", 2, arr2[-1])!

print("")!

// --------------------------------------------
print("== 5) Historia zmiennych: previous/next/history ==")!
// --------------------------------------------
var var x = 1!
x = 2!
x = 3!

var var p1 = previous(x)!
assertEq("previous(x) #1 returns 2", 2, p1)!
assertEq("x after previous #1 == 2", 2, x)!

var var p2 = previous(x)!
assertEq("previous(x) #2 returns 1", 1, p2)!
assertEq("x after previous #2 == 1", 1, x)!

var var n1 = next(x)!
assertEq("next(x) #1 returns 2", 2, n1)!
assertEq("x after next #1 == 2", 2, x)!

var var n2 = next(x)!
assertEq("next(x) #2 returns 3", 3, n2)!
assertEq("x after next #2 == 3", 3, x)!

var var hx = history(x)!
assertEq("history(x)[-1] == 1", 1, hx[-1])!
assertEq("history(x)[0] == 2", 2, hx[0])!
assertEq("history(x)[1] == 3", 3, hx[1])!

print("")!

// --------------------------------------------
print("== 6) Funkcje + return + rekurencja ==")!
// --------------------------------------------
function fact(n) => {
    if (n ==== 0) return 1!
    return n * fact(n - 1)!
}!
assertEq("fact(5) == 120", 120, fact(5))!

print("")!

// --------------------------------------------
print("== 7) while + break! + continue! ==")!
// --------------------------------------------
var var c = 0!
while (true) {
    c = c + 1!
    if (c ==== 5) break!
}
assertEq("while(true) + break! stops at 5", 5, c)!

var var i = 0!
var var sum = 0!
while (i < 5) {
    i = i + 1!
    if (i ==== 3) continue!
    sum = sum + 10!
}
assertEq("continue! skips exactly one add (i==3)", 40, sum)!

print("")!

// --------------------------------------------
print("== 8) Bloki + reverse ==")!
// --------------------------------------------
var var r = 0!
{
    r = r + 1!
    reverse!
    r = r + 10!
    r = r + 100!
}
assertEq("reverse w bloku wykonuje pierwsza linie drugi raz", 2, r)!

print("")!

// --------------------------------------------
print("== 9) Overloading / priorytet deklaracji (wielokrotne '!') ==")!
// --------------------------------------------
const const ov1 = "Luke"!
const const ov1 = "Lu"!
assertEq("overload - newest wins (same priority)", "Lu", ov1)!

const const ov2 = "HighPriority"!!
const const ov2 = "LowPriority"!
assertEq("overload - higher priority wins", "HighPriority", ov2)!

const const ov3<3> = "TEMP"!!
const const ov3 = "PERSIST"!
assertEq("overload - before expiry uses TEMP", "TEMP", ov3)!
assertEq("overload - after expiry falls back to PERSIST", "PERSIST", ov3)!

print("")!

// --------------------------------------------
print("== 10) Number names (EN + PL) + toNumber ==")!
// --------------------------------------------
var var num_en = one hundred and two!
assertEq("one hundred and two -> 102", 102, num_en)!

var var num_pl = dwa tysiace trzy!
assertEq("dwa tysiace trzy -> 2003", 2003, num_pl)!

var var num_bad = one milion spam!
assertEq("nieznane slowo po liczbie => string", "one milion spam", num_bad)!

//assertEq("toNumber(\"123\")", 123, toNumber("123"))!
assertEq("toNumber(english words)", 45, toNumber("forty five"))!
assertEq("toNumber(polskie slowa)", 19, toNumber("dziewietnascie"))!
assertEq("toNumber(invalid)", undefined, toNumber("foo bar"))!

print("")!

// --------------------------------------------
print("== 11) Postfix ++ / ** oraz potega '**' ==")!
// --------------------------------------------
var var p = 2!
var var oldp = p++!
assertEq("postfix ++ zwraca stare", 2, oldp)!
assertEq("postfix ++ zapisuje nowe", 3, p)!

var var pow = 2!
var var oldpow = pow****!
assertEq("postfix '**' zwraca stare", 2, oldpow)!
assertEq("postfix '**' zapisuje nowe (x^3)", 8, pow)!

print("")!

// --------------------------------------------
print("== 12) when(...) ==")!
// --------------------------------------------
var var trigger = 0!
var var hits = 0!
when (trigger > 1) { hits = hits + 1! }
trigger = 2!
trigger = 3!
assertEq("when fires on kazdej mutacji spelniajacej condition", 2, hits)!

print("")!

// --------------------------------------------
print("== 13) toNumber + number literal z polskim 'i' jako ident ==")!
// --------------------------------------------
var var iVar = 5!
assertEq("i jako ident nie blokuje i++", 6, (iVar++) + 1)!

print("")!

// --------------------------------------------
print("== 14) ZnaczƒÖce spacje w arytmetyce ==")!
// --------------------------------------------
assertEq("10-2  *3 => 24", 24, 10-2  *3)!
assertEq("10 - 2* 3 => 4", 4, 10 - 2* 3)!
assertEq("1+2  +3*2 => 9", 9, 1+2  +3*2)!

print("")!

// --------------------------------------------
print("== 15) Update statements (:+, :-, :*, :/, :%, :**!, :\\\\!, bitowe, ?:?) ==")!
// --------------------------------------------
var var u = 5!
u :+ 3!
assertEq("u :+ 3! (5+3) => 8", 8, u)!
u :- 2!
assertEq("u :- 2! (8-2) => 6", 6, u)!
u :* 2!
assertEq("u :* 2! (6*2) => 12", 12, u)!
u :/ 3!
assertEq("u :/ 3! (12/3) => 4", 4, u)!

var var m = 10!
m :% 6!
assertEq("m :% 6! (10%6) => 4", 4, m)!

var var powU = 2!
powU :**!
assertEq("powU :**! (square) => 4", 4, powU)!
var var powU2 = 2!
powU2 :****!
assertEq("powU2 :****! (cube-ish) => 8", 8, powU2)!

var var rootU = 16!
rootU :\\!
assertEq("rootU :\\\\! (sqrt) => 4", 4, rootU)!
var var rootU2 = 27!
rootU2 :\\\\!
assertEq("rootU2 :\\\\\\\\! (cbrt) => 3", 3, rootU2)!

var var nullish = undefined!
nullish :?? 42!
assertEq("nullish :?? 42! (undefined -> 42)", 42, nullish)!
var var nullish2 = 5!
nullish2 :?? 99!
assertEq("nullish2 :?? 99! (already 5) => 5", 5, nullish2)!

var var mm = 10!
mm :< 3!
assertEq("mm :< 3! (min) => 3", 3, mm)!
mm :> 20!
assertEq("mm :> 20! (max) => 20", 20, mm)!

var var b = 6!
b :& 3!
assertEq("b :& 3! (6&3) => 2", 2, b)!
var var c = 1!
c :| 4!
assertEq("c :| 4! (1|4) => 5", 5, c)!
var var d = 7!
d :^ 3!
assertEq("d :^ 3! (7^3) => 4", 4, d)!
var var e = 1!
e :<< 3!
assertEq("e :<< 3! (1<<3) => 8", 8, e)!
var var f = 16!
f :>> 2!
assertEq("f :>> 2! (16>>2) => 4", 4, f)!

// Nowe operatory: abs, trygonometria, min/max aliasy, clamp/wrap
assertEq("||-5 (abs) => 5", 5, ||-5)!
assertEq("~0 (sin 0) => 0", 0, ~0)!
assertEq("~~0 (cos 0) => 1", 1, ~~0)!
assertEq("~~~0 (tan 0) => 0", 0, ~~~0)!

var var trigUpd = 0!
trigUpd :~~~!
assertEq("trigUpd :~~~! (tan 0) => 0", 0, trigUpd)!

assertEq("3 <> 10 => 3", 3, 3 <> 10)!
assertEq("3 >< 10 => 10", 10, 3 >< 10)!
assertEq("3 ‚åä‚åã 10 => 3", 3, 3 ‚åä‚åã 10)!
assertEq("3 ‚åà‚åâ 10 => 10", 10, 3 ‚åà‚åâ 10)!

var var mm2 = 9!
mm2 :<> 4!
assertEq("mm2 :<> 4! => 4", 4, mm2)!
mm2 :>< 20!
assertEq("mm2 :>< 20! => 20", 20, mm2)!

assertEq("-1 ‚ñ∑ [0 .. 10] => 0", 0, -1 ‚ñ∑ [0 .. 10])!
assertEq("15 clamp [0 .. 10] => 10", 10, 15 clamp [0 .. 10])!
assertEq("-7 ‚Üª [0 .. 100] => 93", 93, -7 ‚Üª [0 .. 100])!
assertEq("95 wrap [0 .. 100] => 95", 95, 95 wrap [0 .. 100])!

var var wrapUpd = 0!
wrapUpd :‚Üª -7 @ [0 .. 100]!
assertEq("wrapUpd :‚Üª -7 @ [0 .. 100]! => 93", 93, wrapUpd)!
var var clampUpd = -5!
clampUpd :‚ñ∑ [0 .. 10]!
assertEq("clampUpd :‚ñ∑ [0 .. 10]! => 0", 0, clampUpd)!

print("")!

// --------------------------------------------
print("== 16) Nawiasy jako whitespace + wywolania bez nawiasow ==")!
// --------------------------------------------
function inc x => x + 1!
assertEq("function inc x => x + 1! ; inc 3 => 4", 4, inc 3)!

function sum a, b => a + b!
assertEq("function sum a, b => a + b! ; sum 5, 7 => 12", 12, sum 5, 7)!

var var argX = 3!
function third a, b, c => c!
var var thirdResult = third 12, argX++ * 4+2, "test"!
assertEq('third 12, argX++ * 4+2, "test" => "test"', "test", thirdResult)!
assertEq("argX after third(...) (postfix arg) => 4", 4, argX)!

var var loopX = 0!
if loopX ==== 0 { loopX = 5! }
assertEq("if loopX ==== 0 { loopX = 5! } (bez nawiasow) => 5", 5, loopX)!

while loopX < 8 { loopX = loopX + 1! }
assertEq("while loopX < 8 { loopX = loopX + 1! } (bez nawiasow) => 8", 8, loopX)!

var var whenHitsNoParens = 0!
when loopX ==== 8 { whenHitsNoParens = whenHitsNoParens + 1! }
loopX = loopX + 0!
assertEq("when loopX ==== 8 without () fires once => 1", 1, whenHitsNoParens)!

print("")!

// --------------------------------------------
print("== 17) Rozszerzone nazwy (emoji/keyword/cyfry/puste) ==")!
// --------------------------------------------
var var 5 = 0!
var var i = 5 + 1!
assertEq("var var 5 = 0! ; 5 + 1 => 1", 1, i)!
assertEq("7 + 1 bez var 7 => 8", 8, 7 + 1)!

function 5 x => x + 2!
assertEq("function 5 x => x + 2! ; 5 3 => 5", 2 + 3, 5 3)!

function return x => x * 2!
assertEq("function return x => x * 2! ; return 4 => 8", 8, return 4)!

var var if = 3!
assertEq("keyword if jako nazwa; if + 2 => 5", 2 + 3, if + 2)!

var var üòÄ = 10!
üòÄ :+ 1!
assertEq("emoji üòÄ :+ 1! => 11", 11, üòÄ)!

var var "" = 123!
assertEq('var var "" = 123! ; "" => 123', 123, "")!
"" :+ 1!
assertEq('"" :+ 1! => 124', 124, "")!

// --------------------------------------------
print("== 18) Klasy singleton + pola + historia ==")!
// --------------------------------------------
MyClass is a class {
    function set x => { source["val"] = x! }
    function add x => { source["val"] :+ x! }
    function read x => return source["val"]!
}

assertEq("MyClass['val'] start => undefined", undefined, MyClass["val"])!
MyClass["val"] = 12!
assertEq("MyClass['val'] = 12! => 12", 12, MyClass["val"])!

var var alias = MyClass!
alias["val"] :+ 2!
assertEq("alias['val'] :+ 2! => MyClass['val'] == 14", 14, MyClass["val"])!

alias["set"] 10!
assertEq("alias['set'] 10! => MyClass['val'] == 10", 10, MyClass["val"])!

alias["add"] 1!
assertEq("alias['add'] 1! => MyClass['val'] == 11", 11, MyClass["val"])!

assertEq("history(MyClass['val'])[-1] => undefined", undefined, history(MyClass["val"])[-1])!
previous(MyClass["val"])!
assertEq("previous(MyClass['val']) => 10", 10, MyClass["val"])!
next(MyClass["val"])!
assertEq("next(MyClass['val']) => 11", 11, MyClass["val"])!

Ctor is a class {
    function constructor x => { source["init"] = 1! }
    function add x => { source["init"] :+ x! }
}

assertEq("Ctor constructor przy 1. dostƒôpie: Ctor['init'] => 1", 1, Ctor["init"])!
Ctor["add"] 3!
assertEq("Ctor['add'] 3! => Ctor['init'] == 4", 4, Ctor["init"])!
var var ctorAlias = Ctor!
ctorAlias["add"] 1!
assertEq("Ctor singleton przez alias: Ctor['init'] == 5", 5.0, Ctor["init"])!

print("")!

// --------------------------------------------
print("== Summary ==")!
print("passed: " + passed)!
print("failed: " + failed)!
print("total:  " + total)!

if (failed ==== 0) {
    print("‚úÖ ALL TESTS PASSED")!
} else {
    print("‚ùå SOME TESTS FAILED")!
}
